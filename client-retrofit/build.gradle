import de.bitdroid.jaxrs2retrofit.JaxRs2RetrofitTask
import de.bitdroid.jaxrs2retrofit.converter.AnnotatedParam
import de.bitdroid.jaxrs2retrofit.converter.ParamConverter
import org.jvalue.ods.api.auth.RestrictedTo
import com.squareup.javapoet.ClassName

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'de.bitdroid.jaxrs2retrofit:plugin:0.1.3'
        classpath project(':models')
    }
}

dependencies {
    compile 'com.squareup.retrofit:retrofit:1.8.+'
    compile 'com.squareup.retrofit:converter-jackson:1.8.+'
    compile project(':models')

    testCompile 'org.jmockit:jmockit:1.13'
    testCompile 'junit:junit:4.11'
}

// integration test
def testSrcDir = 'src/integrationtest/java'
def testResourceDir = 'src/integrationtest/resources'
sourceSets {
    integrationTest {
        java.srcDir file(testSrcDir)
        resources.srcDir file(testResourceDir)
        compileClasspath = sourceSets.main.output + configurations.testCompile + sourceSets.test.output
        runtimeClasspath = output + compileClasspath
    }
}
task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}
check.dependsOn integrationTest

task jaxrs2retrofit(type: JaxRs2RetrofitTask) {
    inputDir = new File(project(':server').projectDir.toString() + "/src/main/java/")
    outputDir = new File(project.projectDir.toString() + "/target/generated-sources/jaxrs2retrofit/")
    retrofitPackageName = "org.jvalue.ods.api"
    excludedClassNamesRegex = "PluginApi|FilterDescriptionApi|Admin.*"
    paramConverterManager.registerConverter(ClassName.get(RestrictedTo.class), new ParamConverter() {
        @Override
        AnnotatedParam convert(AnnotatedParam param) {
            return null;
        }
    });

}
project.compileJava.dependsOn jaxrs2retrofit
project.compileJava.source += jaxrs2retrofit.outputs.files

idea {
    module {
        testSrcDir += new File(testSrcDir)
        sourceDirs += new File(project.projectDir.toString() + "/target/generated-sources/jaxrs2retrofit/")
    }
}
